* CODE OBTAINED FROM XEPHON MVS UPDATE AUGUST 2002
* THE NICE THING HERE IS THAT THE SYSTEM LEVEL HASH COMES BACK
*   AS THE RETURN CODE, AND THEREFORE AFTER RUNNING THIS PROGRAM,
*   THE FOLLOWING JOB STEPS CAN BE MADE DEPENDENT ON THIS RETCODE.
         TITLE 'GET OS NAME AND VERSION'
* DISPLAY OPERATING SYSTEM NAME AND VERSION, EG OS/390 021000
* RETURN HASH CODE FORMED FROM PRODUCT NAME (BYTES 0-3), VERSION, AND
* RELEASE, SO THAT YOU CAN CHECK THE RETURN CODE AND VERIFY THE RELEASE
GETOSLVL CSECT
GETOSLVL AMODE 31
GETOSLVL RMODE 24                  RMODE(24) REQUIRED FOR TPUT
         BAKR  14,0                SAVE REGISTERS AND RETURN ADDRESS
         BASR  12,0                SET BASE REGISTER
         USING *,12
         SPACE
         L     1,16                A(CVT)
         USING CVT,1
* R1: ADDRESS OF COMMUNICATIONS VECTOR TABLE
         L     2,CVTECVT           A(ECVT)
         USING ECVT,2
* R2: ADDRESS OF EXTENDED COMMUNICATIONS VECTOR TABLE
         MVC   PNAMEVRL,ECVTPNAM
         LA    1,PNAMEVRL
         LA    0,L'PNAMEVRL
         TPUT  (1),(0)
* FORM HASH CODE FROM PRODUCT NAME (BYTES 0-3), VERSION AND RELEASE
         XC    HASH,HASH           CLEAR <HASH>
         XC    HASH,PRODNAME+0     BYTES 0-1
         XC    HASH,PRODNAME+2     BYTES 2-3
         XC    HASH,PRODVERS       VERSION
         XC    HASH,PRODREL        RELEASE
         NC    HASH,=X'FFF'        MODULO(4096)
         LH    15,HASH
         PR ,                      PROGRAM RETURN
         SPACE
HASH     DS    H
         SPACE
PNAMEVRL DS    0CL22
PRODNAME DS    CL16                PRODUCT (OPERATING SYSTEM) NAME
PRODVERS DS    CL2                 PRODUCT VERSION
PRODREL  DS    CL2                 PRODUCT RELEASE
         DS    CL2                 PRODUCT MODIFICATION LEVEL
         SPACE
         CVT   DSECT=YES
         IHAECVT
         END
